resource "aws_lb_target_group" "frontend_http" {

  name                          = "${var.application_name}-${var.environment_name}-frontend-http"
  port                          = 5000
  protocol                      = "HTTP"
  vpc_id                        = aws_vpc.main.id
  slow_start                    = 0
  load_balancing_algorithm_type = "round_robin"
  target_type                   = "instance"

  stickiness {
    enabled = true
    type    = "lb_cookie"
  }

  health_check {
    enabled             = true
    port                = 5000
    interval            = 30
    protocol            = "HTTP"
    path                = "/"
    matcher             = "200"
    healthy_threshold   = 2
    unhealthy_threshold = 3
    timeout             = 5
  }

}
resource "aws_lb_target_group_attachment" "frontend_http" {

  for_each = aws_instance.frontend

  target_group_arn = aws_lb_target_group.frontend_http.arn
  target_id        = each.value.id
  port             = 5000

}

# Notice that we are dynamically constructing a `list` of subnets using the corresponding `aws_subnet` 
# resource map. When a resource block is provisioned with a `count` that resource block becomes a `list`, 
# when it is provisioned with a `for_each` iterator it becomes a `map`. 
# This is important detail to pay attention to when you want to reference it from other resources.

resource "aws_lb" "frontend" {
  name               = "frontendlb"
  internal           = false
  load_balancer_type = "application"
  subnets            = [for subnet in values(aws_subnet.frontend) : subnet.id]
  security_groups    = [aws_security_group.frontend_lb.id]

  access_logs {
    bucket  = aws_s3_bucket.alb_log_bucket.id
    prefix  = "health-checks" # Optional log file prefix
    enabled = true            # This enables both access logs and health check logs
  }
  tags = {
    Name        = "${var.application_name}-${var.environment_name}-frontend-lb"
    application = var.application_name
    environment = var.environment_name
  }

  # Ensure the bucket policy is in place before enabling logging
  depends_on = [aws_s3_bucket_policy.alb_log_bucket_policy]
}

resource "aws_lb_listener" "frontend_http" {

  load_balancer_arn = aws_lb.frontend.arn
  port              = 80
  protocol          = "HTTP"

  # default_action {
  #   type             = "forward"
  #   target_group_arn = aws_lb_target_group.frontend_http.arn
  # }

  default_action {
    type = "redirect"
    redirect {
      port        = "443"
      protocol    = "HTTPS"
      status_code = "HTTP_301"
    }
  }
}

resource "aws_lb_listener" "frontend_https" {
  load_balancer_arn = aws_lb.frontend.arn
  port              = 443
  protocol          = "HTTPS"
  ssl_policy        = "ELBSecurityPolicy-TLS13-1-2-2021-06"
  certificate_arn   = aws_acm_certificate.main.arn

  default_action {
    type             = "forward"
    target_group_arn = aws_lb_target_group.frontend_http.arn
  }
}

resource "aws_acm_certificate" "main" {
  domain_name               = "localhost.localstack.cloud"
  subject_alternative_names = ["*.localhost.localstack.cloud"]
  validation_method         = "DNS"

  lifecycle {
    create_before_destroy = true
  }
}
